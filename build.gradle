apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'

group = 'com.arkinmodi'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:+'
    }

    // Enable dependency dynamic version lock (buildscript-gradle.lockfile)
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

// Enable dependency dynamic version lock (gradle.lockfile)
dependencyLocking {
    lockAllConfigurations()
}

// Disable dependency dynamic versioning caching
configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:+'))

    testImplementation('org.junit.jupiter:junit-jupiter-api')
    testImplementation('org.assertj:assertj-core:+')

    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
}

test {
    useJUnitPlatform()
}

spotless {
    enforceCheck = true

    format '.gradle', {
        target '*.gradle'
        endWithNewline()
        trimTrailingWhitespace()
    }

    java {
        importOrder('java', 'javax', 'org', 'com', 'com.arkinmodi', '')
        endWithNewline()
        trimTrailingWhitespace()
    }
}
